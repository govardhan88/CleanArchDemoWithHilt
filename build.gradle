// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        kotlinVersion       = "1.3.72"
        lifeCycleVersion    = '2.2.0'
        navigationVersion   = '2.3.0-alpha05'
        //versioning
        generateVersionName = { BigDecimal versionCode ->
            int patch = versionCode.remainder(1000)
            int minor = (versionCode / 1000).remainder(1000)
            int major = (versionCode / 1000000).remainder(1000)
            "$major.$minor.$patch"
        }
        appVersionCode = 1_000_000
        appVersionName = generateVersionName(appVersionCode)

        //Common
        minSdkVersion = 21
        targetSdkVersion = 30
        versionCode = appVersionCode
        versionName = appVersionName
        androidCompileSdkVersion = 30
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        supportVersion      = '1.0.0'
        coreKtxVersion      = '1.3.0'
        multidexVersion     = '2.0.1'
        timberVersion       = '4.7.1'
        coroutineVersion    = '1.3.7'
        paginationVersion   = '2.1.2'
        commonDependencies = { dependencyHandler ->
            delegate = dependencyHandler
            implementation "androidx.multidex:multidex:$multidexVersion"
            implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
            implementation "androidx.core:core-ktx:$coreKtxVersion"
            implementation "androidx.appcompat:appcompat:$supportVersion"
            // Lifecycle
            implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifeCycleVersion"
            implementation "androidx.lifecycle:lifecycle-common-java8:$lifeCycleVersion"
            implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifeCycleVersion"
            kapt "androidx.lifecycle:lifecycle-compiler:$lifeCycleVersion"
            //Timber
            implementation "com.jakewharton.timber:timber:$timberVersion"
            //Coroutines
            implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutineVersion"
            implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutineVersion"
            //Paging
            implementation "androidx.paging:paging-runtime:$paginationVersion"
        }

        //Ui
        constraintLayoutVersion = '2.0.0-beta7'
        materialVersion         = '1.1.0'
        designSupportVersion    = '28.0.0'
        navVersion       = '1.0.0'
        uiDependencies = { dependencyHandler ->
            delegate = dependencyHandler
            implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
            implementation "com.google.android.material:material:$materialVersion"
            // ViewModel
            implementation "androidx.lifecycle:lifecycle-viewmodel:$lifeCycleVersion"
            //navigation
            implementation "android.arch.navigation:navigation-fragment-ktx:$navigationVersion"
            implementation "android.arch.navigation:navigation-ui-ktx:$navVersion"
        }

        //Hilt
        hiltVersion = '2.28.1-alpha'
        hiltViewModelVesrion = '1.0.0-alpha01'
        hiltCompilerVersion = '1.0.0-alpha01'
        hiltDependencies = { dependencyHandler ->
            delegate = dependencyHandler
            implementation "com.google.dagger:hilt-android:$hiltVersion"
            kapt "com.google.dagger:hilt-android-compiler:$hiltVersion"
            implementation "androidx.hilt:hilt-lifecycle-viewmodel:$hiltViewModelVesrion"
            kapt "androidx.hilt:hilt-compiler:$hiltCompilerVersion"
            // Activity KTX for viewModels()
            implementation "androidx.activity:activity-ktx:1.1.0"
        }

        // Retrofit
        retrofit2Version = '2.9.0'
        okhttpLoggingVersion = '4.8.0'
        retrofit = { dependencyHandler ->
            delegate = dependencyHandler
            implementation "com.squareup.retrofit2:retrofit:$retrofit2Version"
            implementation "com.squareup.retrofit2:converter-gson:$retrofit2Version"
            api "com.squareup.retrofit2:converter-gson:$retrofit2Version"
            implementation "com.squareup.okhttp3:logging-interceptor:$okhttpLoggingVersion"
            implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'
        }


    }
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.2.0-alpha04"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hiltVersion"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$navigationVersion"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}